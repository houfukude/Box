apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

def static getBuildTime() {
    String dateTime = System.getenv("BUILD_TIME")
    if (dateTime == null) {
        return new Date().format("yyyyMMdd_HHmm", TimeZone.getTimeZone("GMT+08:00"))
    } else {
        return dateTime
    }
}

String buildTime = getBuildTime()

def static updateServer() {
    return "\"https://api.github.com/repos/" + System.getenv().getOrDefault("REPO", "houfukude/Box") + "/releases/latest\""
}

android {
    //noinspection GradleDependency
    compileSdk 33

    namespace 'com.github.tvbox.osc'
    defaultConfig {


        applicationId 'com.github.tvbox.osc.tk'
        minSdkVersion 19
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 29
        versionCode 1
        versionName "1.0.".concat(buildTime)
        multiDexEnabled true
        buildConfigField "String", "BUILD_TIME", '"' + buildTime + '"'
        //设置room的Schema的位置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildFeatures {
        dataBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    lint {
        disable 'UnsafeOptInUsageError'
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }

    signingConfigs {
        release {
            storeFile = new File(rootProject.projectDir, "TVBoxOSC.jks")
            keyAlias = System.getenv("ALIAS")
            storePassword = System.getenv("KEY_STORE_PASSWORD")
            keyPassword = System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            buildConfigField("String", "SERVER", this.updateServer())
            applicationIdSuffix ".debug"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            minifyEnabled true
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters 'armeabi-v7a'
                //noinspection ChromeOsAbiSupport
                abiFilters 'arm64-v8a'
            }
        }
        release {
            buildConfigField("String", "SERVER", this.updateServer())
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            minifyEnabled true
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters 'armeabi-v7a'
                //noinspection ChromeOsAbiSupport
                abiFilters 'arm64-v8a'
            }
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            def fileName = outputFileName.replace("app", "TVBOX_" + buildTime)
            fileName = fileName.replace("-release", "")
            println fileName
            outputFileName = new File(fileName)
        }
    }

}

dependencies {
    api fileTree(dir: "libs", include: ['*.jar', '*.aar'])
    implementation 'org.conscrypt:conscrypt-android:2.5.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    // for WebDAV
    implementation('com.github.thegrizzlylabs:sardine-android:0.8') {
        exclude group: 'com.squareup.okhttp3'
    }
    implementation 'com.github.NanoHttpd.nanohttpd:nanohttpd:nanohttpd-project-2.3.1'
    implementation 'com.github.NanoHttpd.nanohttpd:nanohttpd:nanohttpd-websocket-2.3.1'
    implementation 'com.google.zxing:core:3.5.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'com.google.android.material:material:1.4.0'
    implementation files('libs/thunder.jar')
    implementation project(path: ':quickjs')

    //noinspection KaptUsageInsteadOfKsp
    kapt 'androidx.room:room-compiler:2.5.1'
    implementation 'com.github.alidili:FlowLayout:v1.5'
    implementation 'androidx.room:room-runtime:2.5.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.squareup.okhttp3:okhttp:3.12.11'
    implementation 'com.squareup.okio:okio:3.3.0'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.49-androidx'
    implementation 'com.kingja.loadsir:loadsir:1.3.8'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    //noinspection KaptUsageInsteadOfKsp
    kapt 'com.github.bumptech.glide:compiler:4.16.0'
    implementation 'me.jessyan:autosize:1.2.1'
    implementation('com.thoughtworks.xstream:xstream:1.4.20') {
        exclude group: 'xmlpull', module: 'xmlpull'
    }
    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation 'com.orhanobut:hawk:2.0.1'
    implementation 'net.sourceforge.streamsupport:android-retrofuture:1.7.4'
    implementation 'androidx.annotation:annotation:1.6.0'
    implementation "androidx.media3:media3-exoplayer:1.1.1"
    implementation "androidx.media3:media3-ui:1.1.1"
    implementation "androidx.media3:media3-exoplayer-dash:1.1.1"
    implementation "androidx.media3:media3-exoplayer-hls:1.1.1"
    implementation "androidx.media3:media3-exoplayer-rtsp:1.1.1"
    implementation "androidx.media3:media3-datasource-rtmp:1.1.1"
    implementation 'androidx.media3:media3-common:1.1.1'
    // To add media3 software decoders and extensions
    //implementation "com.github.anilbeesetti.nextlib:nextlib-mediainfo:0.5.0" // To get media info through ffmpeg



    implementation "com.aliyun.sdk.android:AliyunPlayer:5.5.2.0-full"
    implementation 'com.alivc.conan:AlivcConan:0.9.5'
    implementation 'xyz.doikki.android.dkplayer:dkplayer-ui:3.3.7'
    implementation 'xyz.doikki.android.dkplayer:videocache:3.3.7'

    implementation('org.xwalk:xwalk_shared_library:23.53.589.4') {
        exclude group: 'com.android.support'
    }
    implementation 'com.lzy.net:okgo:3.0.4'
    implementation 'com.owen:tv-recyclerview:3.0.0'
    implementation 'com.blankj:utilcodex:1.31.0'
    implementation 'com.github.getActivity:XXPermissions:18.5'
    implementation 'org.jsoup:jsoup:1.16.1'
    implementation 'com.github.hedzr:android-file-chooser:v1.2.0-final'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation 'com.github.seven332:jcifs:1.3.19'
    implementation 'org.conscrypt:conscrypt-android:2.5.2'
    implementation 'io.github.inflationx:viewpump:2.1.1'
    implementation 'io.github.inflationx:calligraphy3:3.1.1'
    configurations {
        configureEach {
            exclude group: 'xpp3', module: 'xpp3'
        }
    }
}
